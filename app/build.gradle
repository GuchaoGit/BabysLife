apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
//读取签名配置并返回Properties
def buildSign() {
    def Properties buildProperties = new Properties()
    buildProperties.load(new FileInputStream(file("../local.properties")))
    return buildProperties
}
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.guc.babyslife"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10
        versionName "1.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            def Properties buildPro = buildSign()
            storeFile file(buildPro["store_file"])
            keyAlias buildPro["key_alias"]
            keyPassword buildPro["key_password"]
            storePassword buildPro["store_password"]
            // 如果要支持最新版的系统 Android 7.0
            // 这一行必须加，否则安装时会提示没有签名
            // 作用是只使用旧版签名，禁用V2版签名模式
            v2SigningEnabled false
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}
greendao {
    schemaVersion 2 //当前数据库版本
    daoPackage 'com.guc.babyslife.greendao'//设置DaoMaster、DaoSession、Dao包名
    targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
}
repositories {
    flatDir {
        dirs 'libs'
    }
}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //rxjava + rxanroid
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    //json
    implementation 'com.google.code.gson:gson:2.8.5'
    //依赖注入
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.jakewharton:butterknife-compiler:8.8.1'

    //刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0'//没有使用特殊Header，可以不加这行
    //添加greendao数据库支持
    implementation 'org.greenrobot:greendao:3.2.2' // 添加库
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    //腾讯广告
    implementation(name: 'GDTSDK.union.4.90.960', ext: 'aar')

    //PhotoView
    implementation 'com.github.chrisbanes.photoview:library:1.2.4'
    //Glide
    api 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    //视频播放控件
    // required
    implementation 'org.salient.artvideoplayer:artplayer-java:0.7.0'
    // Default control panel: optional
    implementation 'org.salient.artvideoplayer:artplayer-ui:0.7.0'

    //引入图表
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
}

def buildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "BabyLife_${buildTime()}_${variant.name}_V${variant.versionName}.apk"
    }
}
